buildscript {
  repositories {
    jcenter()
    mavenCentral()
    maven { url "https://maven.fabric.io/public" }
  }
  dependencies {
    classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta4'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    classpath 'com.frogermcs.androiddevmetrics:androiddevmetrics-plugin:0.4'
    classpath "io.fabric.tools:gradle:1.21.5"
  }
}

apply plugin: "com.android.application"
apply plugin: 'me.tatarka.retrolambda'
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "com.frogermcs.androiddevmetrics"
apply plugin: "io.fabric"

repositories {
  jcenter()
  mavenCentral()
  maven { url "https://maven.fabric.io/public" }
}

def versionMajor = 1
def versionMinor = 1
def versionPatch = 2
def versionBuild = 0
def buildTime = new Date().format("yyyyMMdd");
def appName = "Hishoot2i"

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  packagingOptions {
    exclude "META-INF/services/javax.annotation.processing.Processor"
    exclude "META-INF/LICENSE.txt"
    exclude "META-INF/NOTICE.txt"
    exclude "META-INF/LICENSE"
    exclude "META-INF/NOTICE"
    exclude ".readme"
    exclude "META-INF/INDEX.LIST"
    exclude "LICENSE.txt"
    exclude "NOTICE.txt"
  }

  defaultConfig {
    applicationId "org.illegaller.ratabb.hishoot2i"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    resValue("string", "tray__authority", "${applicationId}.tray")
    resValue("string", "app_version", "$versionName")
    resValue("string", "app_name", "$appName")
    resValue("string", "app_build", "$buildTime")
    vectorDrawables.useSupportLibrary = true
    resConfig("en")
    archivesBaseName =
        "$appName" + "-v${versionName}" + "-${buildTime}" + "-minApi${minSdkVersion.apiLevel}"
  }

  signingConfigs {
    release {
      Properties prop = loadProp("sign.properties")
      keyAlias prop["keyAlias"]
      keyPassword prop["keyPassword"]
      storeFile file(prop["storeFile"])
      storePassword prop["storePassword"]
    }
  }
  buildTypes {
    debug {
      manifestPlaceholders = [fabric_api_key: ""]
      minifyEnabled false
      buildConfigField "boolean", "USE_CRASHLYTICS", "false"
      ext.enableCrashlytics = false
      debuggable true
    }
    release {
      manifestPlaceholders = [fabric_api_key: loadFromProp("fabric.properties", "apiKey")]
      minifyEnabled true
      shrinkResources true
      zipAlignEnabled true
      signingConfig android.signingConfigs.release
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
      final FileCollection proguardCollection = files {
        file("../buildsystem/proguard").listFiles()
      }
      proguardFiles(proguardCollection)
      buildConfigField "boolean", "USE_CRASHLYTICS", "true"
      ext.enableCrashlytics = true
      debuggable false
    }
  }
  dexOptions { javaMaxHeapSize "2g" }
  lintOptions {
    abortOnError false
    disable "IconDensities", "IconMissingDensityFolder"
  }
}
dependencies {
  def appDependencies = rootProject.ext.dependencies
  compile appDependencies.appCompat
  compile appDependencies.cardView
  compile appDependencies.design
  compile appDependencies.recyclerView
  compile appDependencies.preference
  compile appDependencies.customActivityOnCrash
  compile appDependencies.eventBus
  compile appDependencies.gson
  compile appDependencies.universalImageLoader
  compile appDependencies.dart
  provided appDependencies.dartProcesor
  compile appDependencies.butterknife
  apt appDependencies.butterknifeCompiler
  compile(appDependencies.crashlytics) { transitive = true }
  compile(appDependencies.aboutLibraries) { transitive = true }
  apt appDependencies.daggerCompiler
  compile appDependencies.dagger
  provided appDependencies.javaxAnnotation
  compile appDependencies.tray
  debugCompile appDependencies.leakcanaryDebug
  releaseCompile appDependencies.leakcanaryRelease
  compile appDependencies.bottomBar
  compile appDependencies.smoothProgressBar
  compile appDependencies.materialSearchview
  compile appDependencies.recyclerviewAnimators
  compile appDependencies.rxJava
  compile appDependencies.rxAndroid
  compile appDependencies.swipeRevealLayout
  compile appDependencies.appUpdater
  retrolambdaConfig appDependencies.jakewhartonLambda
}

String loadFromProp(String pathProp, String key) {
  Properties props = loadProp(pathProp)
  if (props.containsKey(key)) {
    return props[key]
  } else {
    throw new GradleException(String.format("key: %s notfound", key))
  }
}

Properties loadProp(String pathProp) {
  Properties props = new Properties()
  def fileProp = file(pathProp)
  if (fileProp.canRead()) {
    props.load(new FileInputStream(fileProp))
  } else {
    throw new GradleException(String.format("missing prop: %s", pathProp))
  }
  return props;
}