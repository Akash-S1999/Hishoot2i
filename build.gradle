buildscript {
    apply from: "buildsystem/versions.gradle"
    apply from: "buildsystem/buildVersion.gradle"
    addRepos(repositories)
    dependencies {
        classpath(classPathNotation.agp) { exclude module: 'proguard-gradle' }
        classpath classPathNotation.kotlin
        classpath(classPathNotation.proguard) { force = true }
    }
}

allprojects {
    addRepos(repositories)
    configurations*.resolutionStrategy {
        eachDependency { detail ->
            if (detail.requested.group == groups.supportLibrary) {
                detail.useVersion(versions.supportLibrary)
            }
            if (detail.requested.group == groups.kotlin) {
                detail.useVersion(versions.kotlin)
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply from: '../buildsystem/ktlint.gradle'
    afterEvaluate {
        project.tasks.findByName("check").dependsOn(ktlint)
        // NOTE: Not have 'test', [yet].
        project.tasks.matching {
            it.name.contains("test") || it.name.contains("Test")
        }.all { it.enabled = false }

    }
}