plugins {
    //
    id 'androidx.navigation.safeargs.kotlin' apply false
    id 'com.android.application' apply false
    id 'dagger.hilt.android.plugin' apply false
    id 'org.jetbrains.kotlin.jvm' apply false

    id 'com.autonomousapps.dependency-analysis'
    id 'com.diffplug.spotless'
    id 'com.github.ben-manes.versions'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven() {
            url 'https://dl.bintray.com/pdvrieze/maven'
            content { includeModuleByRegex(/net\.devrieze/, /xmlutil.*/) }
        }
        gradlePluginPortal()
    }

    ext {
        // region Version
        coilVersion = project.coilVersion
        coroutinesVersion = project.coroutinesVersion
        daggerHiltVersion = project.daggerHiltVersion
        daggerVersion = project.daggerVersion
        desugarVersion = project.desugarVersion
        googleMaterialVersion = project.googleMaterialVersion
        kotlinVersion = project.kotlinVersion
        ktLintVersion = project.ktLintVersion
        leakcanaryVersion = project.leakcanaryVersion
        okHttpVersion = project.okHttpVersion
        serializationVersion = project.serializationVersion
        timberVersion = project.timberVersion
        xmlUtilVersion = project.xmlUtilVersion
        //
        xActivityVersion = project.xActivityVersion
        xAnnotationVersion = project.xAnnotationVersion
        xAppCompatVersion = project.xAppCompatVersion
        xArchCoreVersion = project.xArchCoreVersion
        xBrowserVersion = project.xBrowserVersion
        xCollectionVersion = project.xCollectionVersion
        xConstraintLayoutVersion = project.xConstraintLayoutVersion
        xCoordinatorLayoutVersion = project.xCoordinatorLayoutVersion
        xCoreVersion = project.xCoreVersion
        xCustomViewVersion = project.xCustomViewVersion
        xDocumentFileVersion = project.xDocumentFileVersion
        xDrawerLayoutVersion = project.xDrawerLayoutVersion
        xFragmentVersion = project.xFragmentVersion
        xHiltVersion = project.xHiltVersion
        xLifeCycleVersion = project.xLifeCycleVersion
        xMultiDexVersion = project.xMultiDexVersion
        xNavigationVersion = project.xNavigationVersion
        xRecyclerViewVersion = project.xRecyclerViewVersion
        xRoomVersion = project.xRoomVersion
        //
        hamcrestVersion = project.hamcrestVersion
        jUnitVersion = project.jUnitVersion
        robolectricVersion = project.robolectricVersion
        // endregion
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails drd ->
            def group = drd.requested.group
            def name = drd.requested.name
            if (group ==~ /androidx\.activity/) drd.useVersion xActivityVersion
            if (group ==~ /androidx\.arch\.core/) drd.useVersion xArchCoreVersion
            if (group ==~ /androidx\.collection/) drd.useVersion xCollectionVersion
            if (group ==~ /androidx\.lifecycle/) drd.useVersion xLifeCycleVersion
            if (group ==~ /androidx\.transition/) drd.useVersion xTransitionVersion
            if (group ==~ /com\.squareup\.okhttp3/) drd.useVersion okHttpVersion
            if (group ==~ /org\.jetbrains\.kotlin/) drd.useVersion kotlinVersion
            if (group ==~ /org\.jetbrains\.kotlinx/ && name ==~ /kotlinx-coroutines.*/)
                drd.useVersion coroutinesVersion
        }
    }

    apply from: "$rootDir/buildsystem/spotless.gradle"
}

dependencyAnalysis {
    issues {
        all {
            onAny {
                severity 'fail'
            }
        }
    }
}

tasks.named('dependencyUpdates').configure {
    def isStable = { String version ->
        return version ==~ /^[0-9,.v-]+(-r)?$/
    }
    rejectVersionIf { !isStable(it.candidate.version) && isStable(it.currentVersion) }
    checkForGradleUpdate = false //
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = '1.8'
            allWarningsAsErrors = true
            verbose = true
            useIR = true
        }
    }
}
